plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

repositories {
    mavenCentral()
    maven { url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev" }
    // This is bootstrap wasm repo, remove it as regular atomic-fu will be published
    maven { url "https://maven.pkg.jetbrains.space/kotlin/p/wasm/experimental" }
    // Coroutines from the outer project are published by previous CI buils step
    mavenLocal()
}

kotlin {
    jvm()
    js(IR) {
        nodejs()
    }
    wasmJs() {
        nodejs()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        wasmJsTest {
            dependencies {
                implementation kotlin('test-wasm-js')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
    }
    targets {
        configure([]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

// Drop this configuration when the Node.JS version in KGP is at least 20.2.0
// check it here:
// https://github.com/JetBrains/kotlin/blob/master/libraries/tools/kotlin-gradle-plugin/src/common/kotlin/org/jetbrains/kotlin/gradle/targets/js/nodejs/NodeJsRootExtension.kt
rootProject.extensions.findByType(org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension.class).with {
    it.nodeVersion = "20.2.0"
}